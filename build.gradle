plugins {
    id 'java'
    id 'maven-publish'
    id "io.spring.dependency-management" version "1.1.7"
}

group = 'ilya.project'
version = '0.0.1-SNAPSHOT'

java.sourceCompatibility = JavaVersion.VERSION_17
java.targetCompatibility = JavaVersion.VERSION_17

repositories {
    mavenLocal()
    mavenCentral()
}

def springVersion = '3.3.2'
def feignVersion = '4.1.4'

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-parent:${springVersion}"
    implementation "org.springframework.boot:spring-boot-starter-validation:${springVersion}"
    implementation "org.springframework.boot:spring-boot-starter:${springVersion}"
    implementation "org.springframework.boot:spring-boot-starter-web:${springVersion}"

    implementation "org.springframework.cloud:spring-cloud-starter-openfeign:${feignVersion}"

    // https://mvnrepository.com/artifact/com.jayway.jsonpath/json-path
    implementation 'com.jayway.jsonpath:json-path:2.9.0'


    //configuration metadata
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor:${springVersion}"

    //Aspects
    implementation "org.springframework.boot:spring-boot-starter-aop:${springVersion}"

}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}

tasks.withType(ProcessResources).configureEach {
    filteringCharset = "UTF-8"
}

tasks.named('compileJava') {
    inputs.files(tasks.named('processResources'))
}

def repoDir = "${project.projectDir.toString()}/maven-repo/"

publishing {

    publications {
        maven (MavenPublication) {
            groupId = rootProject.getGroup()
            artifactId = rootProject.getName()
            version = rootProject.getVersion()

            from components.java
        }
    }

    repositories {
        maven {
            url = "file://${repoDir}"
        }
    }
}

gradle.taskGraph.whenReady { taskGraph ->
    if (taskGraph.hasTask(":publish")) {
        def groupId = rootProject.getGroup()
        def artifactId = rootProject.getName()
        def version = rootProject.getVersion()

        def repoPath = "${repoDir}${groupId.replace('.', '/')}/${artifactId}/${version}"
        def artifactPath = new File(repoPath)

        if (artifactPath.exists()) {
            logger.lifecycle("An artifact with version ${version} already exists. Replacing the library with a new version...")

            delete repoPath
        }
    }
}

